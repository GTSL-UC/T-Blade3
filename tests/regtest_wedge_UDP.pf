!
! Regression test for udpHubWedge.c which creates a wedge using the
! blade geometry created by udpTblade.c
!
! Test case uses .csm file which creates a blade and wedge union
!
! TODO: ESP version check?
!
!------------------------------------------------------------------------------
@test
subroutine regtest_udpHubWedge()
    use pfunit_mod
    implicit none

    character(255)              :: shell, serveCSM, ref_file, test_file
    character(1000)             :: read_line
    character(:),   allocatable :: ESPROOT, error_msg, srcESP, dir_path, filename, &
                                   command_1
    integer                     :: funit = 10, ios, n_temp, status, i, j, k, &
                                   npoints, iread, nline_ref, nline_test, &
                                   ibody_ref, ibody_test
    integer,    parameter       :: nspan = 21, nmean = 161
    real                        :: ref_meanlines(3, nspan, nmean), test_meanlines(3, nspan, nmean)
    real                        :: tol1 = 10E-8, mass_props_ref(11), mass_props_test(11)


    !
    ! Locate serveCSM executable and store location in a temporary file
    ! Assumes a ESP/T-Blade3 build rather than a basic ESP build
    !
    call execute_command_line ('which serveCSM > temp.dat')

    ! Read temporary file
    open (funit, file = 'temp.dat')
    read (funit, fmt = '(a)', iostat = ios) serveCSM
    close (funit)

    ! Delete temporary file
    call execute_command_line ('rm temp.dat')



    !
    ! Next steps depending on the value of the I/O status specifier
    ! Indicates whether serveCSM was located or not
    !
    if (ios < 0) then

        ! If serveCSM was not found, raise an error and exit
        print *, 'ERROR": serveCSM not found on the system'
        call exit

    else if (ios == 0) then

        ! If serveCSM was found, extract the location of the ESP
        ! root directory
        ! Assumes that the serveCSM executable pointed to on the
        ! system exists in $ESPROOT/bin
        n_temp                          = index (trim(serveCSM), 'bin/serveCSM')
        ESPROOT                         = serveCSM (1:n_temp - 1)

    end if



    !
    ! Working directory relative path and input file name
    !
    dir_path                            = 'inputs/OpenCSM/Case7-28-8/Rotor/'
    filename                            = dir_path//'tblade_rotor_hubwedge.csm'



    !
    ! Determine which shell we are working with and source
    ! ESP accordingly
    !
    call get_environment_variable ('SHELL', shell)

    ! If running bash/zsh
    if ( index (trim(shell), 'bash') /= 0 .or. &
         index (trim(shell), 'zsh')  /= 0 ) then

        srcESP                          = 'source '//ESPROOT//'ESPenv.sh'

    ! If running csh/tcsh
    else if ( index (trim(shell), 'csh')  /= 0 .or. &
              index (trim(shell), 'tcsh') /= 0) then

        srcESP                          = 'source '//ESPROOT//'ESPenv.csh'

    ! If running fish
    else if ( index (trim(shell), 'fish') /= 0 ) then

        srcESP                          = 'exec bash -c "source '//ESPROOT//'ESPenv.sh; exec fish"'

    ! Any other shell is not supported
    else

        print *, 'ERROR: Unable to identify shell'
        call exit

    end if

    ! Source either ESPenv.sh/ESPenv.csh before running ESP
    ! TODO: If using fish, as of now, the source command needs to
    !       be run before running test suite
    if ( index (trim(shell), 'fish') == 0 ) &
        call execute_command_line (srcESP)



    !
    ! Run ESP using example Case7-28-8/Rotor
    !
    command_1                           = 'serveCSM -batch '//filename//' > run.dat'
    call execute_command_line (command_1)



    !
    ! Read reference (x,y,z) meanlines
    !
    do i = 1, nspan     ! Loop through all spanwise locations

        ! Spanwise reference meanline file name
        write (ref_file, "('regtest_files/meanline.sec', i0, '.Case7-28-8.dat')") i
        ref_file                        = dir_path//ref_file

        ! Open reference meanline files
        open (funit, file = trim(ref_file))

        ! Read (x,y,z) reference meanlines for all spanwise sections
        do j = 1, nmean
            read (funit, *) ref_meanlines(1,i,j), ref_meanlines(2,i,j), ref_meanlines(3,i,j)
        end do

        ! Close file
        close (funit)

    end do  ! i = 1, nspan



    !
    ! Test all spanwise (x,y,z) meanlines
    !
    do i = 1, nspan     ! Loop through all spanwise locations

        ! Spanwise section file name
        write (test_file, "('Tblade3_temp/meanline.sec', i0, '.Case7-28-8.dat')") i

        ! Read all (x,y,z) meanline files created by test
        open (funit, file = trim(test_file))

        do j = 1, nmean

            ! Read (x,y,z) section meanlines
            read (funit, *) test_meanlines(1,i,j), test_meanlines(2,i,j), test_meanlines(3,i,j)

            ! Test meanlines
            @assertEqual (ref_meanlines(1,i,j), test_meanlines(1,i,j), tol1)
            @assertEqual (ref_meanlines(2,i,j), test_meanlines(2,i,j), tol1)
            @assertEqual (ref_meanlines(3,i,j), test_meanlines(3,i,j), tol1)

        end do

        ! Close file
        close (funit)

    end do  ! i = 1, nspan



    !
    ! Read reference log file and determine location of the
    ! mass properties in the file
    !
    open (funit, file = dir_path//'regtest_files/ESP_wedge_run.log')

    ! Line number counter
    iread                               = 0

    ! Read file line by line
    do

        read (funit, '(a)', iostat = ios) read_line
        if (ios /= 0) exit  ! EOF

        ! Advance line number counter
        iread = iread + 1

        ! Mass properties of location in file
        if (index (trim(read_line), 'Mass properties of Bodys on stack') /= 0) &
            nline_ref = iread + 2

        ! Read mass properties from reference log file
        if (iread == nline_ref) then
            backspace (funit)
            read (funit, *) ibody_ref, mass_props_ref
        end if

    end do

    close (funit)   ! Close reference log file



    !
    ! Read log file created by the test and determine location of the
    ! mass properties in the file
    !
    open (funit, file = 'run.dat')

    ! Line number counter
    iread                               = 0

    ! Read file line by line
    do

        read (funit, '(a)', iostat = ios) read_line
        if (ios /= 0) exit  ! EOF

        ! Advance line number counter
        iread = iread + 1

        ! Mass properties of location in file
        if (index (trim(read_line), 'Mass properties of Bodys on stack') /= 0) &
            nline_test                  = iread + 2

        ! Read mass properties from reference log file
        if (iread == nline_test) then
            backspace (funit)
            read (funit, *) ibody_test, mass_props_test
        end if

    end do

    close (funit)   ! Close log file created by test



    ! Test mass properties
    do i = 1, 11
        @assertEqual (mass_props_ref(i), mass_props_test(i), tol1)
    end do

    ! Deletion commands
    call execute_command_line ('rm run.dat')
    call execute_command_line ('rm *.jrnl')
    call execute_command_line ('rm -rf Tblade3_temp')
    call execute_command_line ('rm -rf Tblade3_temp2')


end subroutine regtest_udpHubWedge
!------------------------------------------------------------------------------
